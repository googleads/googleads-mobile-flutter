group 'io.flutter.plugins.googlemobileads'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:7.0.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    dependencies {
      api 'com.google.android.gms:play-services-ads:20.2.0'
      constraints {
        implementation('androidx.work:work-runtime:2.7.0-alpha05') {
          because 'previous versions have a bug impacting this android 12 behavior'
        }
      }
      testImplementation 'junit:junit:4.13.1'
      testImplementation 'org.hamcrest:hamcrest:2.2'
      testImplementation 'org.mockito:mockito-inline:3.9.0'
      testImplementation 'org.robolectric:robolectric:4.4'
    }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

afterEvaluate {
    def containsEmbeddingDependencies = false
    for (def configuration : configurations.all) {
        for (def dependency : configuration.dependencies) {
            if (dependency.group == 'io.flutter' &&
                    dependency.name.startsWith('flutter_embedding') &&
                    dependency.isTransitive())
            {
                containsEmbeddingDependencies = true
                break
            }
        }
    }
    if (!containsEmbeddingDependencies) {
        android {
            dependencies {
                def lifecycle_version = "1.1.1"
                compileOnly "android.arch.lifecycle:runtime:$lifecycle_version"
                compileOnly "android.arch.lifecycle:common:$lifecycle_version"
                compileOnly "android.arch.lifecycle:common-java8:$lifecycle_version"
            }
        }
    }
}
